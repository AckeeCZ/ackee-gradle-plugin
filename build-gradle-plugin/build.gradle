plugins {
    alias libs.plugins.kotlin
    alias libs.plugins.kotlin.dokka
    alias libs.plugins.kotlin.dsl
    alias libs.plugins.nexus.staging
    id("java-gradle-plugin")
    id("maven-publish")
}

dependencies {
    implementation(libs.android.tools.build)
    implementation(libs.gradleVersions)
    implementation(libs.owaspDependencyCheck)
}

ext {
    PUBLISH_ARTIFACT_ID = 'build-gradle-plugin'
    POM_DESCRIPTION = 'Gradle plugin with some useful configuration shared among projects'
}

ext.libProperties = new Properties()
ext.libProperties.load(file("${rootDir}/lib.properties").newReader())

gradlePlugin {
    def idPrefix = "io.github.ackeecz.plugin"
    def commonPackageName = "cz.ackee.gradle.plugin"

    plugins {
        register("ackeeGradlePlugin") {
            id = "${idPrefix}.build"
            implementationClass = "${commonPackageName}.AckeeGradlePlugin"
        }

        register("configureAppPlugin") {
            id = "${idPrefix}.config"
            implementationClass = "${commonPackageName}.ConfigureAppPlugin"
        }

        register("dependenciesCheckPlugin") {
            id = "${idPrefix}.dependenciescheck"
            implementationClass = "${commonPackageName}.DependenciesCheckPlugin"
        }

        register("deploymentPlugin") {
            id = "${idPrefix}.deployment"
            implementationClass = "${commonPackageName}.DeploymentPlugin"
        }

        register("variantsPlugin") {
            id = "${idPrefix}.variants"
            implementationClass = "${commonPackageName}.VariantsPlugin"
        }

        register("verificationsPlugin") {
            id = "${idPrefix}.verifications"
            implementationClass = "${commonPackageName}.VerificationsPlugin"
        }
    }
}


apply from: "${rootProject.projectDir}/gradle/mavencentral/publish.gradle"
